options {
    MULTI = true;
    STATIC = false;
    VISITOR = true;
}

PARSER_BEGIN(Petri)
package org.dhbw.ka.ml.generated;

public class Petri {
}
PARSER_END(Petri)

SKIP : {
    " "
|   "\r"
|   "\t"
|   "\n"
}

SPECIAL_TOKEN : {
    < ONE_LINE_COMMENT: "#" (~["\n"])* "\n" >
}

TOKEN : {
    < STRUCT: "struct" >
|   < BOOL: "bool" >
|   < INT32: "int32" >
|   < INT64: "int64" >
|   < FLOAT32: "float32" >
|   < FLOAT64: "float64" >
|   < STRING: "string" >
|   < LIST: "list" >
|   < DELETED: "@deleted" >
}

TOKEN : {
    < #DIGIT: ["0"-"9"] >
|   < DIGITS: (<DIGIT>)+ >
|   < #LETTER: ["a"-"z","A"-"Z"] >
|   < #ID_ANY: (<DIGIT>|<LETTER>|"_") >
|   < ID: (("_")+ (<DIGIT>|<LETTER>) (<ID_ANY>)* | <DIGITS> (<LETTER>|"_") (<ID_ANY>)* | <LETTER> (<ID_ANY>)*) >
}

ASTroot root() :
{
}
{
    (complex_type())* #ComplexTypes <EOF> { return jjtThis; }
}

void complex_type() #void :
{}
{
    struct()
}

void struct():
{
    ASTIdentifier ident;
    ASTIdentifier parent = null;
}
{
    <STRUCT> ident=identifier(false) ((":" parent=identifier(false))?) "{" ((field())*) #fields "}"
    {
        jjtThis.setIdent(ident);
        jjtThis.setParent(parent);
    }
}

void field():
{
    Token t = null;
    SimpleNode type;
    ASTIdentifier ident;
}
{
    (t=field_annotation())? type=type_identifier() ident=identifier(false) ";"
    {
        jjtThis.setDeleted(t != null);
        jjtThis.setType(type);
        jjtThis.setIdent(ident);
    }
}

SimpleNode type_identifier() #void :
{
    SimpleNode t;
}
{
    (
    t=primitive_type(false)
    |
    t=identifier(false)
    |
    t=list()
    ) { return t; }
}

Token field_annotation() #void :
{
    Token t;
}
{
    (t=<DELETED>) { return t; }
}

ASTPrimitiveType primitive_type(boolean b) #PrimitiveType(b) :
{
    Token t;
}
{
    (
    t=<BOOL>
    |
    t=<INT32>
    |
    t=<INT64>
    |
    t=<FLOAT32>
    |
    t=<FLOAT64>
    |
    t=<STRING>
    )
    {
        jjtThis.setType(t.image);
        jjtThis.setBeginColumn(t.beginColumn);
        jjtThis.setBeginLine(t.beginLine);
        return jjtThis;
    }
}

ASTIdentifier identifier(boolean b) #Identifier(b) :
{
    Token t;
}
{
    (t=<ID>)
    {
        jjtThis.setIdent(t.image);
        jjtThis.setBeginColumn(t.beginColumn);
        jjtThis.setBeginLine(t.beginLine);
        return jjtThis;
    }
}

ASTList list() #List : {}
{
    <LIST> "<" type_identifier() ">" { return jjtThis; }
}